package rules.basic;

import live.healthy.facts.dto.BodyInfoDto;
import live.healthy.facts.dto.BodyTypeDto;
import live.healthy.facts.model.DailyActivity;

import live.healthy.facts.model.body_desc_enums.Shoulders;
import live.healthy.facts.model.body_desc_enums.BodyLook;
import live.healthy.facts.model.body_desc_enums.BodyTendations;
import live.healthy.facts.model.body_desc_enums.Forearms;
import live.healthy.facts.model.body_desc_enums.WeightTendations;


global Integer ecto_meso;
global Integer meso_endo;

rule "Ectomorph - mesomorph"
    salience ecto_meso
    activation-group "body-bodyTypeEnum"
    when
        $bi: BodyInfoDto(shoulders in (Shoulders.NARROWER_THAN_HIPS.value, Shoulders.SAME_AS_HIPS.value),
                        forearms in (Forearms.SMALL.value, Forearms.AVERAGE.value),
                        bodyLook in (BodyLook.LONG_AND_NARROW.value, BodyLook.SQUARE_AND_RUGGED.value),
                        weightTendations in (WeightTendations.CAN_NOT_GAIN.value,
                        WeightTendations.CAN_GAIN_AND_LOSE.value),
                        bodyTendations in (BodyTendations.SKINNY.value, BodyTendations.LEAN_YET_MUSCULAR.value)) and
        $bodyTypeDto: BodyTypeDto(type == "")
    then
        $bodyTypeDto.setType("ECTOMORPH_MESOMORPH");
        $bodyTypeDto.setDescription("YOUR BODY IS BETWEEN AN ECTOMORPH AND A MESOMORPH.");
        update($bodyTypeDto);
end

rule "Mesomorph - Endomorph"
    salience meso_endo
    activation-group "body-bodyTypeEnum"
    when
        $bi: BodyInfoDto(shoulders in (Shoulders.WIDER_THAN_HIPS.value, Shoulders.SAME_AS_HIPS.value),
                        forearms in (Forearms.BIG.value, Forearms.AVERAGE.value),
                        bodyLook in (BodyLook.ROUND_AND_SOFT.value, BodyLook.SQUARE_AND_RUGGED.value),
                        weightTendations in (WeightTendations.CAN_GAIN_CAN_NOT_LOSE.value,
                        WeightTendations.CAN_GAIN_AND_LOSE.value),
                        bodyTendations in (BodyTendations.CARRY_EXTRA_FAT.value, BodyTendations.LEAN_YET_MUSCULAR.value)) and
        $bodyTypeDto: BodyTypeDto(type == "")
    then
        $bodyTypeDto.setType("ENDOMORPH_MESOMORPH");
        $bodyTypeDto.setDescription("YOUR BODY IS BETWEEN AN MESOMORPH AND A ENDOMORPH.");
        update($bodyTypeDto);
end


rule "Nonexistent bodyTypeEnum"
    activation-group "body-bodyTypeEnum"
    salience 1
    when
        $bodyTypeDto: BodyTypeDto(type == "")
    then
        $bodyTypeDto.setType("NONEXISTENT");
        $bodyTypeDto.setDescription("Information about your body is contradictory. We cannot determine your type.");
        update($bodyTypeDto);
end